=== PARAM-VALUE

Value Name::
   PARAM-VALUE

Purpose::
  This parameter value type is the basic value type for parameter values.

Format Definition::

[source,abnf]
----
param-value   = paramtext / quoted-string

paramtext     = *SAFE-CHAR
quoted-string = DQUOTE *QSAFE-CHAR DQUOTE

QSAFE-CHAR    = WSP / %x21 / %x23-7E / NON-US-ASCII
    ; Any character except CONTROL and DQUOTE

SAFE-CHAR     = WSP / %x21 / %x23-2B / %x2D-39 / %x3C-7E
             / NON-US-ASCII
    ; Any character except CONTROL, DQUOTE, ";", ":", ","

NON-US-ASCII  = UTF8-2 / UTF8-3 / UTF8-4
     ; UTF8-2, UTF8-3, and UTF8-4 are defined in [RFC3629]

CONTROL       = %x00-08 / %x0A-1F / %x7F
     ; All the controls except HTAB
----

Description::
  Values that contain the COLON, SEMICOLON, or COMMA character separators **MUST**
  be specified as quoted-string text values.

  Property parameter values that contain the DQUOTE character **MUST** be escaped
  and specified as quoted-string text values.

  Property parameter values that are not in quoted-strings are case-insensitive.

  An intentional line break **MUST** be represented by the sequence of "\n" or "\N"
  (BACKSLASH followed by a LATIN SMALL LETTER N (U+006E) or
  a LATIN CAPITAL LETTER N (U+004E)).


Example::
  The value `cid:part1.0001@example.org` in the parameter `ALTREP` within a
  `DESCRIPTION` property in iCalendar can be specified like this:

----
DESCRIPTION;ALTREP="cid:part1.0001@example.org":The Fall'98 Wild
 Wizards Conference - - Las Vegas\, NV\, USA
----

==== Normalization

Values of the PARAM-VALUE parameter data type **MUST** convert all line breaks ("\n" or "\N") into
the character sequence "\n" (BACKSLASH followed by a LATIN SMALL LETTER N (U+006E)).

If a value of the PARAM-VALUE type is specified as `paramtext` (i.e., not inside a quoted-string),
the value should be down-cased.
